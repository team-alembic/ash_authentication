defmodule AshAuthentication.Jwt.Config do
  @moduledoc """
  Implementation details JWT generation and validation.

  Provides functions to generate token configuration at runtime, based on the
  resource being signed for and for verifying claims and checking for token
  revocation.
  """

  alias Ash.Resource
  alias AshAuthentication.{Info, Jwt, TokenRevocation}
  alias Joken.{Config, Signer}

  @doc """
  Generate the default claims for a specified resource.
  """
  @spec default_claims(Resource.t()) :: Joken.token_config()
  def default_claims(resource) do
    config =
      resource
      |> config()

    {:ok, vsn} = :application.get_key(:ash_authentication, :vsn)

    vsn =
      vsn
      |> to_string()
      |> Version.parse!()

    Config.default_claims(default_exp: token_lifetime(config))
    |> Config.add_claim(
      "iss",
      fn -> generate_issuer(vsn) end,
      &validate_issuer/3
    )
    |> Config.add_claim(
      "aud",
      fn -> generate_audience(vsn) end,
      &validate_audience(&1, &2, &3, vsn)
    )
    |> Config.add_claim(
      "jti",
      &Joken.generate_jti/0,
      &validate_jti/3
    )
  end

  @doc """
  The generator function used to generate the "iss" claim.
  """
  @spec generate_issuer(Version.t()) :: String.t()
  def generate_issuer(vsn) do
    "AshAuthentication v#{vsn}"
  end

  @doc """
  The validation function used to validate the "iss" claim.

  It simply verifies that the claim starts with `"AshAuthentication"`
  """
  @spec validate_issuer(String.t(), any, any) :: boolean
  def validate_issuer(claim, _, _), do: String.starts_with?(claim, "AshAuthentication")

  @doc """
  The generator function used to generate the "aud" claim.

  It generates an Elixir-style `~>` version requirement against the current
  major and minor version numbers of AshAuthentication.
  """
  @spec generate_audience(Version.t()) :: String.t()
  def generate_audience(vsn) do
    "~> #{vsn.major}.#{vsn.minor}"
  end

  @doc """
  The validation function used to validate the "aud" claim.

  Uses `Version.match?/2` to validate the provided claim against the current
  version.  The use of `~>` means that tokens generated by versions of
  AshAuthentication with the the same major version and at least the same minor
  version should be compatible.
  """
  @spec validate_audience(String.t(), any, any, Version.t()) :: boolean
  def validate_audience(claim, _, _, vsn) do
    Version.match?(vsn, Version.parse_requirement!(claim))
  end

  @doc """
  The validation function used to the validate the "jti" claim.

  This is done by checking that the token is valid with the token revocation
  resource.  Requires that the subject's resource configuration be passed as the
  validation context.  This is automatically done by calling `Jwt.verify/2`.
  """
  @spec validate_jti(String.t(), any, %{resource: module} | any) :: boolean
  def validate_jti(jti, _claims, %{resource: resource}) do
    case Info.tokens_revocation_resource(resource) do
      {:ok, revocation_resource} ->
        TokenRevocation.valid?(revocation_resource, jti)

      _ ->
        false
    end
  end

  def validate_jti(_, _, _), do: false

  @doc """
  The signer used to sign the token on a per-resource basis.
  """
  @spec token_signer(Resource.t()) :: Signer.t()
  def token_signer(resource) do
    config = config(resource)

    algorithm = Keyword.get_lazy(config, :signing_algorithm, &Jwt.default_algorithm/0)

    case Keyword.fetch(config, :signing_secret) do
      {:ok, secret} ->
        Signer.create(algorithm, secret)

      :error ->
        raise "Missing JWT signing secret. Please see the documentation for `AshAuthentication.Jwt` for details"
    end
  end

  defp token_lifetime(config) do
    hours =
      config
      |> Keyword.get_lazy(:token_lifetime, &Jwt.default_lifetime_hrs/0)

    hours * 60 * 60
  end

  defp config(resource) do
    config =
      resource
      |> Info.tokens_options()
      |> Enum.reject(&is_nil(elem(&1, 1)))

    :ash_authentication
    |> Application.get_env(Jwt, [])
    |> Keyword.merge(config)
  end
end
