defmodule AshAuthentication.Strategy.Password.HashPasswordChange do
  @moduledoc """
  Set the hash based on the password input.

  Uses the configured `AshAuthentication.HashProvider` to generate a hash of the
  user's password input and store it in the changeset.

  You can use this change in your actions where you want to change the user's
  password.  If you're not using one of the actions generated by the password
  strategy then you'll need to manually pass the strategy name in the changeset
  context.  Eg:

  ```elixir
  Changeset.new(user, %{})
  |> Changeset.set_context(%{strategy_name: :password})
  |> Changeset.for_update(:update, params)
  |> Accounts.update()
  ```

  or by adding it statically to your action definition:

  ```elixir
  update :change_password do
    change set_context(%{strategy_name: :password})
    change AshAuthentication.Strategy.Password.HashPasswordChange
  end
  ```
  """

  use Ash.Resource.Change
  alias Ash.{Changeset, Error.Framework.AssumptionFailed, Resource.Change}
  alias AshAuthentication.Info

  @doc false
  @impl true
  @spec change(Changeset.t(), keyword, Change.context()) :: Changeset.t()
  def change(changeset, _opts, context) do
    changeset
    |> Changeset.before_action(fn changeset ->
      with {:ok, strategy} <- find_strategy(changeset, context),
           value when is_binary(value) <-
             Changeset.get_argument(changeset, strategy.password_field),
           {:ok, hash} <- strategy.hash_provider.hash(value) do
        Changeset.change_attribute(changeset, strategy.hashed_password_field, hash)
      else
        :error ->
          raise AssumptionFailed, message: "Error hashing password."

        _ ->
          changeset
      end
    end)
  end

  defp find_strategy(changeset, context) do
    with :error <- Info.strategy_for_action(changeset.resource, changeset.action.name),
         :error <- Map.fetch(changeset.context, :strategy_name),
         :error <- Map.fetch(context, :strategy_name) do
      :error
    else
      {:ok, strategy_name} when is_atom(strategy_name) ->
        Info.strategy(changeset.resource, strategy_name)

      {:ok, strategy} ->
        {:ok, strategy}
    end
  end
end
